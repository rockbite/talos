plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    alias(libs.plugins.lombok)
}

dependencies {
    implementation libs.libgdx
    implementation libs.libgdx.tools
    implementation libs.slf4j.api
    implementation libs.logback.core
    implementation libs.logback.classic
}

task packageSources(type: Jar, dependsOn: [classes, jar]) {
    from delombok
    from sourceSets.main.resources.srcDirs
    archiveClassifier = 'web'
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

artifacts {
    api(jar, packageSources)
}

configurations {
    web {
        canBeConsumed true
        canBeResolved false
        extendsFrom implementation, runtimeOnly
    }
}



publishing {
    repositories {
        maven {
            name = "snapshot"
            url = getSnapshotRepositoryUrl()
            credentials(org.gradle.api.credentials.PasswordCredentials)
        }
        maven {
            name = "release"
            url = getReleaseRepositoryUrl()
            credentials(org.gradle.api.credentials.PasswordCredentials)
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact packageSources
            groupId project.group
            artifactId project.name
            version project.version
            pom {
                name = "Talos Tools"
                description = "External tools for TalosVFX"
                url = "https://talosvfx.com"
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'tomski'
                        name = 'Tom Wojciechowski'
                        email = 'tom@asidik.com'
                    }
                    developer {
                        id = 'azakhary'
                        name = 'Avetis Zakharyan'
                        email = 'azakhary@rockbitegames.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/rockbite/talos.git'
                    developerConnection = 'scm:git:ssh://github.com/rockbite/talos.git'
                    url = 'github.com/rockbite/talos/'
                }
            }
        }
    }
}
signing {
    required { project.isReleaseBuild() }
    sign publishing.publications.mavenJava
}
